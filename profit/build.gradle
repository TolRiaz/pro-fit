plugins {
	id 'java'
	id 'war'
	id 'org.springframework.boot' version '2.7.12'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id "com.github.node-gradle.node" version "5.0.0"
}

group = 'pf'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '11'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

def frontendDir = "${projectDir}/src/main/frontend"
node {
	version = '20.2.0'
	npmVersion = '9.6.6'
	download = true
	nodeProjectDir = file("${frontendDir}")
}

sourceSets {
	main {
		resources {
			srcDirs = ["${frontendDir}/dist", "$projectDir/src/main/resources"]
		}
	}
}

processResources {
	dependsOn 'buildAngular'
}

task buildAngular(type: NpmTask) {
	dependsOn "installAngular"
	workingDir = file( "${frontendDir}" )
	inputs.dir "${frontendDir}"
	// Add task to the standard build group
	group = BasePlugin.BUILD_GROUP
	args = ['run', 'build']
}

task installAngular(type: NpmTask) {
	dependsOn "npmInstall"
	workingDir = file( "${frontendDir}" )
	inputs.dir "${frontendDir}"
	group = BasePlugin.BUILD_GROUP
	args = ['install', '-g', '@angular/cli@16.0.4']
}

war {
	enabled = true
	archiveFileName = 'pro-fit.war'
	webAppDirectory = file("${frontendDir}/dist/pro-fit")
	// rootSpec.exclude('index.html')
}